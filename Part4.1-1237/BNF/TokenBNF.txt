/* ENSC 251 -- Fall 2023 -- Simon Fraser University */

/* Here's the definition for some tokens for our subset-C grammar */
/* Other tokens, such as for character and string literals, are left as an exercise */

/* Whitespace is not allowed inside the tokens below, though of course a character literal 
   can contain one and a string can contain multiple whitespace characters */

	gen_assignment_operator : '=' | '/=' | '+=' | '-=' 
						;
						
	int_assignment_operator : '%=' | '<<=' | '>>='
							;

/* We are also going to consider the ternary operator, '?' */
	ternary_operator : '?'
				 ;
										
	shift_operator   : '<<' | '>>'
					 ;
				
	additive_operator : '+' | '-'
					  ;
				
	div_operator : '/'
				 ;
				 
	mod_operator : '%'
				 ;
			 
/* complement */
	comp_operator  : '~'
				    ;				   
				   			
	postfix_operator : '++' | '--'
					 ;

/*
id stands for identifier which begins with "letter" and followed by zero or more instances of "letdig".
*/
    id 			: letter {letdig}*
    			;

/*
int_literal can be one or more digits.
*/
    int_literal_int	: digit
    			| digit int_literal_int
    			;
    			
/*
	int_literal_char	: **** fill this in ****
*/    			

/*
double_literal production rule would can begin with either "real" or "int_literal". After that is decided, next thing to check
would be presense of exponent followed by suffix. Note that suffix is not a mandatory part as it can evaluate to "empty".
*/
   	numeric_literal_double : double_literal_base
   				| double_literal_base suffix
   				;
   				
   	double_literal_base : real
   				| real exponent
   				| int_literal_int exponent
   				;

/*
real production can have"
	- period followed by int_literal
	- int_literal followed by period
	- int_literal followed by period followed by int_literal
*/
   	real        :   '.' int_literal_int
                |   int_literal_int '.'
                |   int_literal_int '.' int_literal_int
                ;
/*
In exponent production rule, 'e' or 'E' is simply a character. Any of following 6 production rule is valid for exponent.
*/
   	exponent    : 'e' '+' int_literal_int  /*  (note: not empty, the letter 'e' ) */
                | 'e' '-' int_literal_int
                | 'e' int_literal_int
                | 'E' '+' int_literal_int
                | 'E' '-' int_literal_int
                | 'E' int_literal_int 
                ;
/*
As far as our course project is concerned, suffix can only be 'd' or 'D'.
*/
    suffix      :  'd' | 'D'
    			;
/*
letdig can be either digit or letter.
*/
    letdig 		: digit | letter
    			;

    letter 		: '_' | 'a' | . . . | 'z' | 'A' | . . . | 'Z'
    			;

    digit 		: '0' | . . . | '9'
    			;

